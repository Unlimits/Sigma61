C  файл QMDQT.for   
C cодержит подпрограмму QMDQT
C ================================================================
***************************************************************************************
*************  Преобразование фактор-графа в алгоритме минимальной степени  
*************************************************************************************************
*     Подпрограмма преобразует факто-граф после исключения очередного узла
* 
*     входные параметры:
*     root - только что исключенный узел. Он становится представителемн нового суперузла
*     (xadj,adjncy) - структура смежности.
*    (rchsze,rchset) - достижимое множество узла root в старом фактор-графе
*     nbrhd - окрестность root, будет слитас ними при формировании нового супер узла
*    marker - вектор маркировки
* 
*     изменяемый параметр:
*     adjncy - переходит в adjncy для фактор графа
C =================================================================
C =============== начало кода QMDQT ==============================
C ================================================================
      SUBROUTINE QMDQT(ROOT,XADJ,ADJNCY,MARKER,
     1                        RCHSZE,RCHSET,NBRHD)
**************************************************************************************************
      integer adjncy(1),marker(1),rchset(1),nbrhd(1)
      integer xadj(1),inhd,irch,j,jstrt,jstop,link
     1            nabor,node,rchsze,root
*                    
**************************************************************************************************    
*
      irch=0
      inhd=0
      node=root
100   jstrt=xadj(node)
      jstop=xadj(node+1)-2 
! print*,'in qmdqt',jstrt,jstop
      if(jstop.lt.jstrt) goto 300
*                        
*            Поместить достижимые узлы в список смежности node            
*                            
      do 200 j=jstrt,jstop
             irch=irch+1
             adjncy(j)=rchset(irch)
             if(irch.ge.rchsze) goto 400
200   continue
*
*            Переход на другой сегмент памяти, занимаемый nbrhd
*
300   link=adjncy(jstop+1)
      node=-link
      if(link.lt.0) goto 100
      inhd=inhd+1
      node=nbrhd(inhd)
      adjncy(jstop+1)=-node
      goto 100
*
*            Все достижимые узлы размещены. Закончить список смежности
*            Добавить root к списку соседей каждого узла из достижимого множества
*                            
400   adjncy(j+1)=0
      do 600 irch=1,rchsze
             node=rchset(irch)
             if(marker(node).lt.0) goto 600
             jstrt=xadj(node)
             jstop=xadj(node+1)-1
             do 500 j=jstrt,jstop
                    nabor=adjncy(j)
                    if(marker(nabor).ge.0) goto 500
                    adjncy(j)=root
                    goto 600
500          continue    
600   continue
      end
C =================================================================
C =================== конец кода QMDQT ============================
C =================================================================
C конец файла  QMDQT.for       